plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false

    //Quilt
    id "io.github.juuxel.loom-quiltflower" version "1.7.3" apply false
    id "org.quiltmc.quilt-mappings-on-loom" version "4.2.1"

    //Building
    id "io.github.pacifistmc.forgix" version "1.2.2"
    id "io.github.p03w.machete" version "1.1.4"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${rootProject.minecraft_version}+build.${rootProject.quilt_mappings}:v2"))
            it.officialMojangMappings {
                setNameSyntheticMembers(false)
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }
}

forgix {
    group = "${rootProject.maven_group}.${rootProject.archives_base_name}"
    mergedJarName = "${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}.jar"
    outputDir = "build/libs/Merged"
}

machete {
    additionalTasks.add("mergeJars")
}